if (project.isModule as boolean) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
//使用greendao
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'gradle_plugin'

def dbflow_version = "4.1.2"

android {

    signingConfigs {
        release {
            storeFile file(KEY_PATH)
            storePassword KEY_PASS
            keyAlias ALIAS_NAME
            keyPassword ALIAS_PASS
        }
        debug {

        }
    }

    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "com.example.cgz.bloodsoulnote2"
        minSdkVersion 21
        targetSdkVersion 28 // 26
        versionCode 1
        versionName project.BLOODSOULTEST as String
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        flavorDimensions "1.0"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig = signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {
        wandoujia {
        }
        qihu360 {
        }
        baidu {
        }
        productFlavors.all {
            flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
        }
    }

    android.applicationVariants.all { variant ->
//        def file = variant.outputs[0].outputFile
//        variant.outputs[0].outputFile = new File(file.parent,
//                file.name.replace(".apk", "-" + defaultConfig.versionName + "-" + releaseTime() + ".apk"))

        variant.outputs.all { output ->  // each 改为 all
            def outFile = output.outputFile
            if (outFile != null && outFile.name.endsWith('.apk')) {
                outputFileName = outFile.name.replace(".apk", "-" + defaultConfig.versionName + "-" + releaseTime() + ".apk")
            }
        }
    }

    dataBinding {
        enabled true
    }

    sourceSets.main {
        jniLibs.srcDir 'libs'
        jni.srcDirs = [] //disable automatic ndk-build call
    }

    //greendao配置
    greendao {
        //版本号，升级时可配置
        schemaVersion 1
        daoPackage 'com.example.cgz.bloodsoulnote2.otherframe.greendao'
        targetGenDir 'src/main/java'
    }

}

static def releaseTime() {
    return new Date().format("yyyy-MM-dd")
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation files('libs/universal-image-loader-1.9.5.jar')
    implementation files('libs/amigo.jar')
    implementation 'com.jcodecraeer:xrecyclerview:1.5.9'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.jakewharton:butterknife:8.8.1'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    implementation 'jp.wasabeef:glide-transformations:4.0.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'
    implementation rootProject.ext.dependencies.support_v4
    implementation 'com.squareup.okhttp3:okhttp:3.12.0'
    implementation 'com.squareup.okhttp:okhttp:2.7.5'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.1.0'
    implementation 'com.google.code.gson:gson:2.8.4'
    testImplementation 'junit:junit:4.12'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.orhanobut:logger:1.4'
    implementation 'org.greenrobot:greendao:3.2.0'
    annotationProcessor "com.github.Raizlabs.DBFlow:dbflow-processor:${dbflow_version}"
    implementation "com.github.Raizlabs.DBFlow:dbflow-core:${dbflow_version}"
    implementation "com.github.Raizlabs.DBFlow:dbflow:${dbflow_version}"
    implementation "com.github.Raizlabs.DBFlow:dbflow-sqlcipher:${dbflow_version}"
    implementation 'com.google.dagger:dagger:2.11'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'
    implementation project(':vitamio')
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.alibaba:arouter-api:1.3.1'
    annotationProcessor 'com.alibaba:arouter-compiler:1.1.4'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.7'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.blankj:utilcode:1.25.9'
}
